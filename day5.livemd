# Advent 2025 - Day 5

```elixir
Mix.install([
  {:kino, "~> 0.17.0"}
])
```

## Setup

```elixir
input = Kino.Input.textarea("Please paste your input file")
```

## Part 1

```elixir
[ranges, ingredients] = input |> Kino.Input.read() |> String.split("\n\n")

ranges = ranges
  |> String.split("\n")
  |> Enum.map(fn range ->
    [from, to] = String.split(range, "-")

    (Integer.parse(from) |> elem(0))..(Integer.parse(to) |> elem(0))
  end)

ingredients = ingredients
  |> String.split("\n")
  |> Enum.map(fn ingredient ->
    Integer.parse(ingredient) |> elem(0)
  end)

[ranges, ingredients]
```

```elixir
ingredients
|> Enum.map(fn ingredient ->
  ranges
  |> Enum.any?(fn range ->
    ingredient in range
  end)
end)
|> Enum.filter(&(&1))
|> Enum.count()
```

## Part 2

```elixir
defmodule RangeSmush do
  def smush_ranges([range | ranges]) do
    smush_ranges([range], ranges)
  end

  def smush_ranges(existing, [range | rest]) do
    to_smush = Enum.find(existing, fn r ->
      not Range.disjoint?(range, r)
    end)

    if to_smush do
      case smush(to_smush, range) do
        :ignore ->
          smush_ranges(existing, rest)
        :replace ->
          existing = existing |> Enum.filter(&(&1 != to_smush))
          smush_ranges(existing, [range | rest])
        range ->
          smush_ranges(existing, [range | rest])
      end
    else
      smush_ranges([range] ++ existing, rest)
    end
  end

  def smush_ranges(existing, []) do
    existing
  end

  def smush(existing, new) do
    e1..e2//_ = existing
    n1..n2//_ = new

    cond do
      # if new is within existing, ignore
      e1 <= n1 and e2 >= n2 -> :ignore

      # if new is over existing, replace
      n1 < e1 and n2 > e2 -> :replace

      # if new starts before existing, trimmed range
      n1 < e1 -> n1..(e1 - 1)

      # if new ends after existing, trimmed range
      n2 > e2 -> (e2 + 1)..n2
    end
  end
end

smushed_ranges = RangeSmush.smush_ranges(ranges)
```

```elixir
smushed_ranges |> Enum.map(&(Range.size(&1))) |> Enum.sum()
```
